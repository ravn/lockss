
parser grammar TdbQueryParser;

@parser::header {
/* ===================================================================

WARNING - WARNING - WARNING - WARNING - WARNING - WARNING - WARNING

If you are modifying a Java file with this warning in it, you are
making changes in the wrong place. This Java file was generated by
ANTLR from a grammar file. You should be editing the grammar file
instead.

=================================================================== */

package org.lockss.tdb;
}

options { tokenVocab = TdbQueryLexer; }

query          : orExpression EOF ;
orExpression   : orExpression OR andExpression #Or2
               | andExpression # Or1
               ;
andExpression  : andExpression AND expression # And2
               | expression # And1
               ;
expression     : PAREN_OPEN orExpression PAREN_CLOSE # ExprSub
               | IDENTIFIER IS NOT? SET # ExprSet
               | IDENTIFIER ( IS NOT? | EQUALS | NOT_EQUALS ) STRING # ExprString
               | IDENTIFIER ( MATCHES | DOES_NOT_MATCH ) STRING # ExprRegex
               ;
