
lexer grammar TdbQueryLexer;

@lexer::header {
/* ===================================================================

WARNING - WARNING - WARNING - WARNING - WARNING - WARNING - WARNING

If you are modifying a Java file with this warning in it, you are
making changes in the wrong place. This Java file was generated by
ANTLR from a grammar file. You should be editing the grammar file
instead.

=================================================================== */

package org.lockss.tdb;
}

@lexer::members {

  void processString() {
    String txt = getText();
    if (txt.indexOf('\\') < 0) {
      setText(txt.substring(1, txt.length() - 1));
      return;
    }
    StringBuilder sb = new StringBuilder();
    for (int i = 1 ; i < txt.length() - 1 ; ++i) {
      char ch = txt.charAt(i);
      if (ch != '\\') {
        sb.append(ch);
      }
    }
    setText(sb.toString());
  }

}

AND            : 'and' ;
OR             : 'or' ;
IS             : 'is' ;
NOT            : 'not' ;
SET            : 'set' ;
EQUALS         : '=' ;
NOT_EQUALS     : '!=' ;
MATCHES        : '~' ;
DOES_NOT_MATCH : '!~' ;
PAREN_OPEN     : '(' ;
PAREN_CLOSE    : ')' ;
STRING         : '"' ( '\\' [\\"] | ~[\\"] )* '"' {processString();}
               | '\'' ( '\\' [\\'] | ~[\\'] )* '\'' {processString();}
               ;
IDENTIFIER     : ( ( 'au' | 'title' | 'publisher' ) ':' )? [a-zA-Z] [a-zA-Z0-9_.]* ( '[' [a-zA-Z0-9] [a-zA-Z0-9_./]* ']' )? ;
WHITESPACE     : [ \t\r\n]+ -> skip ;
